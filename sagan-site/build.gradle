plugins {
	id 'java'
	id 'maven-publish'
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id "org.asciidoctor.convert" version '1.5.9.2'
	id "com.gorylenko.gradle-git-properties" version '2.2.2'
}

apply plugin: 'io.spring.dependency-management'

group = 'io.spring.sagan'
version = "1.0.0-SNAPSHOT"
sourceCompatibility = '1.8'

ext {
	snippetsDir = file('build/generated-snippets')
	springCloudContractVersion = "2.2.3.RELEASE"
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile project(':sagan-client')

	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile 'org.hibernate:hibernate-java8'
	compile 'org.springframework.boot:spring-boot-starter-data-redis'
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-validation'
	compile 'org.springframework.boot:spring-boot-starter-hateoas'
	compile 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-oauth2-client'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// for use in on-the-fly database setup and migrations
	compile 'org.flywaydb:flyway-core'
	compile 'org.apache.httpcomponents:httpclient'
	compile 'org.modelmapper:modelmapper:2.3.8'
	compile 'org.jsoup:jsoup:1.9.2'
	compile 'org.yaml:snakeyaml'
	// for XML manipulation
	compile 'org.xmlbeam:xmlprojector:1.4.17'
	// for use in generating blog atom feeds
	compile 'com.rometools:rome:1.15.0'
	// for use in serving redirects; see urlrewrite.xml
	compile 'org.tuckey:urlrewritefilter:4.0.4'

	// Thymeleaf is used as the template engine for serving HTML
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time'
	implementation 'com.github.mxab.thymeleaf.extras:thymeleaf-extras-data-attribute'

	// for CloudFoundry service binding
	compile "io.pivotal.cfenv:java-cfenv-boot:2.2.2.RELEASE"

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	// datasource and connection pool dependencies
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'com.h2database:h2'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testCompile "org.testcontainers:testcontainers:1.14.3"
	testCompile "org.testcontainers:junit-jupiter:1.14.3"

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation(platform("org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"))
	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
}

test {
	useJUnitPlatform()
	outputs.dir snippetsDir
}

asciidoctor {
	attributes 'snippets': snippetsDir
	inputs.dir snippetsDir
	dependsOn test
}

bootJar {
	dependsOn asciidoctor
	from("${asciidoctor.outputDir}/html5") {
		into 'static/restdocs'
	}
}

task stubsJar(type: Jar) {
	archiveClassifier.set("stubs")
	into('/sagan/site/webapi/') {
		include('**/*Metadata.*')
		from("${project.projectDir}/src/main/java/sagan/site/webapi/")
		from("${project.buildDir}/classes/java/main/sagan/site/webapi/")
	}
	into("META-INF/${project.group}/${project.name}/${project.version}/mappings") {
		include('**/*.*')
		from("${project.buildDir}/generated-snippets/stubs")
	}
}

build.dependsOn("stubsJar")

publishing {
	publications {
		myPublicationName(MavenPublication) {
			artifact stubsJar
		}
	}
}